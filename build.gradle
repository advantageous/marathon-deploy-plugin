buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

group 'io.advantageous.gradle'
version '3.0.6'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"
apply plugin: "java-gradle-plugin"

sourceCompatibility = 1.8

repositories {
    jcenter()
}

project.task("wrapper", type: Wrapper) {
    gradleVersion = '3.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testCompile group: 'junit', name: 'junit', version: '4.4'
}

publishing {
    repositories {
        maven {
            url "build/repository"
        }
    }
}

gradlePlugin {
    plugins {
        "marathon-deploy-plugin" {
            id = "io.advantageous.marathon"
            implementationClass = "io.advantageous.gradle.MarathonDeployPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/advantageous/marathon-deploy-plugin'
    vcsUrl = 'https://github.com/advantageous/marathon-deploy-plugin.git'
    description = 'Deploy your application to an Apache Marathon cluster.'
    tags = ['marathon', 'mesos', 'deploy', 'continuous delivery']

    plugins {
        marathonDeployPlugin {
            id = 'io.advantageous.marathon'
            displayName = 'Marathon Deployment Plugin'
        }
    }
}

signing {
    required false
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                try {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                } catch (MissingPropertyException ignore) {
                }
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                try {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                } catch (MissingPropertyException ignore) {
                }
            }

            pom.project {
                packaging 'jar'
                url 'https://github.com/advantageous/marathon-deploy-plugin'
                name 'io.advantageous.marathon'
                description "Deploy your application to an Apache Marathon cluster."

                scm {
                    url 'scm:git@github.com:advantageous/marathon-deploy-plugin.git'
                    connection 'scm:git@github.com:advantageous/marathon-deploy-plugin.git'
                    developerConnection 'scm:git@github.com:advantageous/marathon-deploy-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'sailorgeoffrey'
                        name 'Geoffrey Chandler'
                    }
                }
            }
        }
    }
}
